#################################################################
## Iro
################################################################ 
##
## * Press Ctrl + '+'/'-' To Zoom in
## * Press Ctrl + S to save and recalculate... 
## * Documents are saved to web storage.
## * Only one save slot supported.
## * Matches cannot span lines.
## * Unicode chars must be defined in \u0000 to \uffff format.
## * All matches must be contained by a single group ( ... )
## * Look behinds not permitted, (?<= or (?<!
## * Look forwards are permitted (?= or (?!
## * Constants are defined as __my_const = (......)
## * The \= format allows unescaped regular expressions
## * Constants referenced by match \= $${__my_const}
## * Constants can reference other constants
## * You are free to delete all the default scopes.
## * Twitter : ainslec , Web: http://eeyo.io/iro
##
################################################################

name                   = voxelscript
file_extensions []     = vs;

################################################################
## Constants
################################################################

__CONTROL_FLOW \= (return|break|continue|import|if|else|for|while|throw|export)(?!\w)
__KEYWORDS \= (true|false|void|any|this|int|char|float|bool|string|import|const|trait|init|class|return|break|continue|implement|private|on|new|is|not|if|else|for|while|throw|export|typedef|=>)(?!\w)
__IDENTIFIER \= (\b[a-zA-Z_][a-zA-Z_0-9]*)(?!\w)
__NONCAPTURE_IDENTIFIER \= (?:\b[a-zA-Z_][a-zA-Z_0-9]*)(?!\w)
__NONCAPTURE_TYPE \= (?:$${__NONCAPTURE_IDENTIFIER}(?:\s*<[\s\w<>\[\],]+>)?(?:\s*[\[\]])*)
__NUMERIC \= (\b\d+(\.\d+)?)(?!\w)

################################################################
## Styles
################################################################

styles [] {

.white : style {
   color                 = #D4D4D4
   textmate_scope        = keyword.operator
}

.variable : style {
   color                 = #9CDCFE
   textmate_scope        = support.variable
}

.string : style {
   color                 = #CE9178
   textmate_scope        = string
}

.keyword : style {
   color                 = #569CD6
   textmate_scope        = keyword
}

.type : style {
   color                 = #4EC9B0
   textmate_scope        = support.type
}

.func : style {
   color                 = #DCDCAA
   textmate_scope        = support.function
}

.special : style {
   color                 = #C586C0
   textmate_scope        = keyword.control
}

.comment : style {
   color                 = #6A9955
   textmate_scope        = comment
}

.numeric : style {
   color                 = #B5CEA8
   textmate_scope        = constant.numeric
}

.escaped : style {
   color                 = #D7BA7D
   textmate_scope        = constant.character.escape
}

.null : style {
   color                 = #F44747
   textmate_scope        = token.error-token
}

}

#################################################
## Parse contexts
#################################################

contexts [] {

##############################################
## Main Context - Entry point context
##############################################

main : context {
   // Color control flow and import/export statements vibrantly
   : pattern {
      regex          \= (\s*)$${__CONTROL_FLOW}
      styles []       = .white, .special;
   }
   
   // Color all _ white, ie underscore parameters
   : pattern {
      regex          \= (\b_\b)
      styles []       = .white;
   }
   
   // Color top-level block headers
   : inline_push {
      regex          \= (implement|class|trait)
      styles []       = .keyword;
      : pop {
         regex       \= ({)
         styles []    = .white;
      }
      : pattern {
         regex       \= (::)
         styles []    = .white;
      }
      : pattern {
         regex       \= $${__KEYWORDS}
         styles []    = .keyword;
      }
      : pattern {
         regex       \= $${__IDENTIFIER}
         styles []    = .type;
      }
      : include "comments";
      : pattern {
         regex       \= (.)
         styles []    = .white;
      }
   }
   
   // Color all "typedef NEW_CUSTOM_TYPE = CUSTOM_TYPE;"
   : inline_push {
      regex          \= (\btypedef\s+)$${__IDENTIFIER}(\s*=)(?=\s*$${__NONCAPTURE_TYPE}\s*;)
      styles []       = .keyword, .type, .white;
      : pop {
         regex       \= (?=\s*;)
         styles []    = .null;
      }
      : include "type";
   }
   
   // Color beginning of "(typedef NEW_CUSTOM_TYPE) = args => CUSTOM_TYPE;"
   : pattern {
      regex          \= (\btypedef\s+)$${__IDENTIFIER}
      styles []       = .keyword, .type;
   }
   
   // Color end of "typedef NEW_CUSTOM_TYPE = args (=> CUSTOM_TYPE);", along with the custom return type of lambdas (=> CUSTOM_TYPE)
   : inline_push {
      // Check for variable declaration in this line
      regex          \= (=>)(?=\s*$${__NONCAPTURE_TYPE}\s*[;{])
      styles []       = .keyword;
      : pop {
         regex       \= (?=\s*[;{])
         styles []    = .null;
      }
      : include "type";
   }
   
   // Color all "new CUSTOM_TYPE"
   : inline_push {
      regex          \= (\bnew)(\s+)(?=$${__NONCAPTURE_TYPE}(?:\s*\())
      styles []       = .keyword, .white;
      : pop {
         regex       \= (\s*\()
         styles []    = .white;
      }
      : include "type";
   }
   
   // Color all "is (not)? TYPE;"
   : pattern {
      regex          \= (is\s*)(not\s*)?(?:$${__KEYWORDS}|$${__IDENTIFIER})
      styles []       = .keyword, .keyword, .keyword, .type;
   }
   
   // Color "CUSTOM_TYPE VARIABLE" accordingly
   : inline_push {
      // Check for variable declaration in this line
      regex          \= (^|[\(,])(?=\s*$${__NONCAPTURE_TYPE}(\s*)$${__IDENTIFIER}(\s*[,);=]))
      styles []       = .white;
      : pop {
         regex       \= (\s*)$${__IDENTIFIER}(?=\s*[,);=])
         styles []    = .white, .variable;
      }
      : include "type";
   }
   
   // Color all special keywords with the keyword color
   : pattern {
      regex          \= $${__KEYWORDS}
      styles []       = .keyword;
   }
   
   // Color "CUSTOM_TYPE func(" accordingly
   : pattern {
      regex          \= $${__IDENTIFIER}(\s+)$${__IDENTIFIER}(?=\s*[(])
      styles []       = .type, .white, .func;
   }
   
   // Color "func(" accordingly
   : pattern {
      regex          \= $${__IDENTIFIER}(?=\s*[(])
      styles []       = .func;
   }
   
   // Color cast operation "<CUSTOM_TYPE> EXPRESSION"
   : inline_push {
      regex          \= (<)\s*(?=$${__NONCAPTURE_TYPE}(?:\s*\>\s*))
      styles []       = .white;
      : pop {
         regex       \= (\s*>)
         styles []    = .white;
      }
      : include "type";
   }
   
   // Color namespaces as a type "ns::ns::"var
   : pattern {
      regex          \= $${__IDENTIFIER}(::)
      styles []       = .type, .white;
   }
   
   // Color all remaining constants as variables
   : pattern {
      regex          \= $${__IDENTIFIER}
      styles []       = .variable;
   }
   
   // Color all numerics
   : pattern {
      regex          \= $${__NUMERIC}
      styles []       = .numeric;
   }
   
   // Color all strings
   : inline_push {
      regex          \= (\")
      styles []       = .string;
      : pop {
         regex       \= (\")
         styles []    = .string;
      }
      : pattern {
         regex       \= (\\x[0-9a-fA-F]{2})
         styles []    = .escaped;
      }
      : pattern {
         regex       \= (\\.)
         styles []    = .escaped;
      }
      : pattern {
         regex       \= (.)
         styles []    = .string;
      }
   }
   
   // Color all chars
   : inline_push {
      regex          \= (\')
      styles []       = .string;
      : pop {
         regex       \= (\')
         styles []    = .string;
      }
      : pattern {
         regex       \= (\\x[0-9a-fA-F]{2})
         styles []    = .escaped;
      }
      : pattern {
         regex       \= (\\.)
         styles []    = .escaped;
      }
      : pattern {
         regex       \= (.)
         styles []    = .string;
      }
   }
   
   // Color comments
   : include "comments";
   
   // Color all other symbols white
   : pattern {
      regex          \= (.)
      styles []       = .white;
   }
}

#################################################
## End of Main Context
#################################################

###########################################
## Multi Line Comment Context
###########################################

comments : context {
   description        = multiline
   : inline_push {
      regex          \= (/\*)
      styles []       = .comment;
      default_style   = .comment
      : pop {
         regex       \= (\*/)
         styles []    = .comment;
      }
   }
   
   : pattern {
      regex          \= (//.*)
      styles []       = .comment;
   }
}

type : context {
   : inline_push {
      regex          \= (\s*)(?:$${__KEYWORDS}|$${__IDENTIFIER})
      styles []      = .white, .keyword, .type;
      : pop {
         // Pop if there's no template parameter
         regex          \= (?!\s*<)
         styles []      = .white;
      }
      : include "template_list";
   }
}

template_list : context {
   : inline_push {
      regex          \= (\s*<)
      styles []      = .white;
      : pop {
         regex       \= (\s*(?:[^\s\w\[\],<])(?:\s*[\[\]])*)
         styles []    = .white;
      }
      : pattern {
         regex       \= (\s*)$${__KEYWORDS}
         styles []    = .white, .keyword;
      }
      : pattern {
         regex       \= (\s*)$${__IDENTIFIER}
         styles []    = .white, .type;
      }
      : pattern {
         regex       \= (\s*[\[\],])
         styles []    = .white;
      }
      : include "template_list";
   }
}
   
}
